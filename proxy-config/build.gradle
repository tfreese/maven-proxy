plugins {
    id "java-library"
}

configurations {
    jaxb
}

//checkstyle {
//    checkstyleTest.enabled = false
//}

dependencies {
    jaxb "com.sun.xml.bind:jaxb-xjc"
    jaxb "com.sun.xml.bind:jaxb-impl"

//    compile(files(genJaxb.classesDir).builtBy(genJaxb))

    api("jakarta.xml.bind:jakarta.xml.bind-api")
}

sourceSets {
    main {
        java {
            srcDir(layout.buildDirectory.get().dir("generated-sources").dir("jaxb"))
        }
    }
}

tasks.register("genJaxb") {
    group = "My Tasks"

    def resourcesDir = layout.projectDirectory.dir("src").dir("main").dir("resources").dir("xsd")
    def schema = resourcesDir.file("proxy-config.xsd")
    inputs.file schema
    def binding = resourcesDir.file("schema.xjb")
    inputs.file binding

    def destDir = layout.buildDirectory.get().dir("generated-sources").dir("jaxb")
    outputs.dir destDir

    doLast {
        mkdir destDir

        ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath

            xjc(destdir: destDir,
                    schema: schema,
                    binding: binding,
                    package: "de.freese.maven.proxy.config",
                    encoding: compileJava.options.encoding,
                    readonly: true,
                    extension: true) {
                arg(value: "-npa")
                //schema(dir: destDir, includes: "*.xsd")
            }
        }
    }
}
compileJava.dependsOn "genJaxb"
