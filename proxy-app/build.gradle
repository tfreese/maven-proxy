buildscript {
    // For Plugins
    repositories {
        mavenLocal()
        mavenCentral()
//        gradlePluginPortal() // maven { url "https://plugins.gradle.org/m2"; name "plugins-gradle" }
    }
}

plugins {
    id "java"
    id "io.spring.dependency-management" version "$version_springDependencyManagement"
}

group = "de.freese.maven.proxy"
version = "0.0.1-SNAPSHOT"
description = "Proxy application project"

repositories {
    mavenLocal()
    mavenCentral()
}

test {
    useJUnitPlatform()
}

clean {
    doLast {
        println("delete " + layout.projectDirectory.dir("app"))
        delete layout.projectDirectory.dir("app")
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$version_springBoot"
    }
}

dependencies {
    implementation("${group}:proxy-core:${version}")

    runtimeOnly("org.apache.logging.log4j:log4j-slf4j2-impl")

    testImplementation("org.junit.jupiter:junit-jupiter")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")

//    runtimeOnly("org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0")
//
//    testImplementation("org.junit.jupiter:junit-jupiter:5.10.0")
//    testRuntimeOnly("org.junit.platform:junit-platform-launcher:1.10.0")
}

tasks.register("build-app") {
    group = "Proxy"

    doLast {
        println("build proxy")

        copy {
            from layout.projectDirectory.dir("service").file("maven-proxy.sh")
            into layout.projectDirectory.dir("app").dir("maven-proxy").dir("bin")
        }

        copy {
            from configurations.runtimeClasspath
            into layout.projectDirectory.dir("app").dir("maven-proxy").dir("libs")
        }

        copy {
            from layout.projectDirectory.dir("src").dir("main").dir("resources")
            include "log4j2.xml"
            include "proxy-config.xml"
            into layout.projectDirectory.dir("app").dir("maven-proxy").dir("resources")
        }
    }
}

tasks.register("deploy-app") {
    group = "Proxy"

    doLast {
        println("deploy proxy")

//        sync {
//            from layout.projectDirectory.dir("app").dir("maven-proxy").dir("bin")
//            into "/opt/jvmapps/maven-proxy/bin"
//        }

        ant.lifecycleLogLevel = "INFO"

        ant.sync(todir: "/opt/jvmapps/maven-proxy/bin", overwrite: true, verbose: true) {
            fileset(dir: layout.projectDirectory.dir("app").dir("maven-proxy").dir("bin"))
        }

        ant.sync(todir: "/opt/jvmapps/maven-proxy/libs", overwrite: true, verbose: true) {
            fileset(dir: layout.projectDirectory.dir("app").dir("maven-proxy").dir("libs"))
        }

        ant.sync(todir: "/opt/jvmapps/maven-proxy/resources", overwrite: true, verbose: true) {
            fileset(dir: layout.projectDirectory.dir("app").dir("maven-proxy").dir("resources"))
        }
    }
}

//tasks.register("deleteAppFolder", Delete) {
//    description "Delete app-Folder."
//    group = "Maintenance"
//
//    logger.lifecycle "delete app-Folder: ${projectDir}/app"
//
//    delete fileTree("app") {
//        include "**/*.*"
//    }
//}
//clean.configure { finalizedBy deleteAppFolder }
